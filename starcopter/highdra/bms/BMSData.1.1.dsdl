# BMS Data

uavcan.time.SynchronizedTimestamp.1.0 timestamp         # 56 bit
starcopter.highdra.bms.StateFSM.0.3 fsm                 # fsm current status, uint8
@assert _offset_ % 32 == {0}                            # align

uint32 pack_sn
@assert _offset_ % 32 == {0}                            # align

uint16 set_id
int4 set_index
bool latch_discharge
bool latch_charge
bool hs_enable
bool connected
bool button
void7
@assert _offset_ % 32 == {0}                            # align

starcopter.highdra.bms.Error.1.1 error_state            # current errors + self flag; sealed 3*8=24 bit
starcopter.highdra.bms.Location.0.2 location            # decoded location from vkey measurement; uint8
@assert _offset_ % 32 == {0}                            # align

uint16 vpack_mv
int8 temperature_c_min
int8 temperature_c_max
float32 ibat_a
float32 ihs_a
uint16 vcells_mv_min
uint16 vcells_mv_max
float32 power_w_filtered
float32 soc
@assert _offset_ % 32 == {0}                            # align
uint16 vbat_mv


@extent 63 * 8